---
layout:  post
title:   Exam 1 Recap
tagline: Solved examples

exercises: assets/recap1-varA.hs
optional:  assets/recap1-varB.hs
---

# Format

The first exam will test your ability to solve problems using three methods:

* recursion
* list comprehension
* higher order functions  (map, filter, fold)


There will be **4 exercises**, each of them specify which method you *have* to use:

1. recursion
2. list comprehension
3. higher order functions
4. any method you want

The exam will last **one hour**.



# Warm-up example


Make a function `incrPos` that takes a list of integers and

* increments positive ones
* removes negative ones

```haskell
> incrPos [1, -17, 0, 3]
[2, 4]
```



All methods share the same signature:

```haskell
incrPos :: [Int] -> [Int]
```



## Recursion

```haskell
incrPos (x:xs)
  | x > 0       = x + 1 : incrPos xs
  | otherwise   = incrPos xs  -- for x <= 0
incrPos [] = []  -- base case
```



## List Comprehension

```haskell
incrPos l = [x + 1 | x <- l, x > 0]
```



## Higher Order Functions

Explicit functions

```haskell
incrPos l = map incr (filter isPos l)
  where incr x = x + 1
        isPos x = x > 0
```

Lambdas

```haskell
incrPos l = map (\x -> x+1) (filter (\x -> x>0) l)
```

Slices

```haskell
incrPos l = map (+1) (filter (>0) l)
```

Composition

```haskell
incrPos l = map (+1) . filter (>0) $ l
```

Implicit arguments

```haskell
incrPos = map (+1) . filter (>0)
```

Arrow

```haskell
import Control.Arrow
incrPos = filter (>0) >>> map (+1)  -- first we filter positives then we increment
```





# Variant A

## 1 Divisors

List of divisors of given number

```haskell
> divisors 6
[1, 2, 3, 6]
```

*Using recursion*

```haskell
divsorsR :: Int -> [Int]
divsorsR n = helper 1  -- start with candidate 1
  where helper cand
          | cand == n          = [n]
          | n `mod` cand == 0  = cand : helper (cand+1)
          | otherwise          = helper (cand+1)  -- move to the next candidate
```



## 2 Odd divisors

List of divisors of given number

```haskell
> oddDivisors 6
[1, 3]
```


*Using list comprehension*

```haskell
divsorsLC :: Int -> [Int]
divsorsLC n = [ cand | cand <- [1..n], n `mod` cand == 0, odd cand ]
```



**Bonus:** _using higher order functions_

```haskell
divisorsHOF :: Int -> [Int]
divisorsHOF n = filter (\cand -> n `mod` cand == 0 && odd cand) [1..n]
```



## 3 Perfect

Whether given number is perfect, ie equal to the sum of its divisors except itself

```haskell
> isPerfect 6
True   -- 6  == 1 + 2 + 3

> isPerfect 10
False  -- 10 \= 1 + 2 + 5
```

*Using higher order functions*

```haskell
isPerfect :: Int -> Bool
isPerfect n = n == sum (filter(\cand -> n `mod` cand == 0) [1..n-1])
```



## 4 Perfect differences

Whether the sum of differences of consecutive elements is a perfect number

```haskell
> perfDiffs [3, 4, 9]
True   -- [3-4, 4-9] ~> [1, 5] ~> 6

> perfDiffs [1, 5, 8, 20]
False  -- [5-1, 8-5, 20-8] ~> [4, 3, 12] ~> 19
```

*Solution*

```haskell
perfDiffs :: [Int] -> Bool
perfDiffs = consecPairs >>>  map pairDiff >>> sum >>> isPerfect
  where consecPairs l = tail l `zip` l  -- [1, 2, 3] ~> [2, 3] `zip` [1, 2, 3] ~> [(2, 1), (3, 2)]
        pairDiff (a, b) = a - b

-- without Control.Arrow
perfDiffs' :: [Int] -> Bool
perfDiffs' l = isPerfect . sum . map pairDiff $ consecPairs
  where consecPairs = tail l `zip` l
        pairDiff = uncurry (-)  -- uncurry converts a curried function to a function on pairs
```





# Variant B - optional

## 1 Product of differences

Product of the differences between consecutive elements

```haskell
> diffsProd [3, 1, 4, 2]
-12  -- [3, 1, 4, 2] ~> (3-1) * (1-4) * (4-2) ~> -12
```

*Using recursion*

```haskell
diffsProd :: [Int] -> Int
diffsProd []  = 1  -- multiplication identity
diffsProd [_] = 1  -- only one element left, can't compute any more differences
diffsProd (x:xs) = (x - head xs) * diffsProd xs
```



## 2 Signs inside an interval

Signs of numbers in given list that belong inside the interval [-9, 9]

```haskell
> signsInterv [5, 19, -2, 0]
"+-0"
```

*Using list comprehension*

```haskell
signsInterv :: [Int] -> String
signsInterv l = [sign x | x <- l, isInInterval x]
  where sign x
          | x > 0     = '+'
          | x < 0     = '-'
          | x == 0    = '0'
        isInInterval x = x >= -9 && x <= 9
```



**Bonus:** _using recursion_

```haskell
-- sign and s
signsInterv [] = ""
signsInterv (x:xs)
  | isInInterval x   = sign x : signsInterv xs
  | otherwise        = signsInterv xs
```

**Bonus:** _using higher order functions_
```haskell
signsInterv' = map sign . filter isInInterval

import Control.Arrow  -- another version
signsInterv  = filter isInInterval >>> map sign
```



## 3 Vowels in even words

Total number of vowels of words of even length

```haskell
> evenVowels ["ana", "are", "mere", "si", "pere"]
5  -- ["ana", "are", "mere", "si", "pere"] ~> ["mere", "si", "pere"] ~> ["ee", "i", "ee"] ~> [2, 1, 2] ~> 5
```

*Using higher order functions*

```haskell
evenVowels :: [String] -> Int
evenVowels = filter (even . length) >>> map onlyVowels >>> map length >>> sum
    where isVowel = (`elem` "aeiou")
          onlyVowels = filter isVowel
```



## 4 Prime factors

List of prime factors of given number

```haskell
> primeFactors 315
[3, 3, 5, 7]  -- 315/3 ~> 105/3 ~> 35/5 ~> 7/7 ~> 1
```

*Solution*

```haskell
primeFactors :: Int -> [Int]
primeFactors 1 = []  -- one has no factors but it is not prime
primeFactors n
  | factors == []   = [n] -- no factors, the number is prime
  | otherwise       = smFact : primeFactors (n `div` smFact)  -- divide by the smallest factor
    where smFact  = head factors
          factors = filter (\d -> (n `mod` d) == 0) [2..n-1]
```
