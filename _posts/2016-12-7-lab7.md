---
layout:  post
title:   Lab 7
tagline: IO, do notation

exercises: https://www.dropbox.com/sh/3od5yhi60h0qo84/AACN0-dlnm8PxQnsF60IS6d0a?dl=1
optional:  https://www.dropbox.com/sh/0ujfjcxjv9sondu/AABBr2VsR4eoM09Lot4TsVxNa?dl=1
---

# Output

`putStr :: String -> IO ()`  prints the given string to stdout:

```haskell
> putStr "Hello, World!\n"
Hello, World!
```

It takes a `String` and does an `IO` action which produces no result, indicated by the empty parentheses `()`. 

Multiple actions can be chained together using the `>>` operator:

```haskell
> putStr "Hello" >> putChar ',' >> putStrLn " World!"
Hello, World!
```



# Input

`getLine :: IO String`  consumes one line from from stdin:

```haskell
> getLine
abc  -- input followed by <enter>
"abc"  -- the result from calling getLine
```

If the input string would be `"abc\nxy"`, after calling `getLine`,  `"xy"` would be left

Multiple actions can be combined using the bind operator `>>=`:

```haskell
> getLine >>= \str -> putChar (head str)
abc  -- input followed by <enter>
a  -- just the first letter
```



# Input-Output

A function which yells everything you enter

```haskell
loudEcho :: IO ()
loudEcho = getLine >>= \line ->
             if line == "." then  -- stop the loop on period
               return ()  -- because loudEcho always returns an IO ()
             else
               putStr (map toUpper line) >>  -- "yell" the input
               putStrLn "!" >>
               loudEcho  -- keep looping
```

`return :: a -> IO a` does nothing, but produces a value. For the input string `"abc"`, calling `return "!"` would produce `"!"` and leave the input unchanged (`"abc"`).

```haskell
> loudEcho
hello  -- input followed by <enter>
HELLO!!
world  -- input followed by <enter>
WORLD!!
.   -- break the loop
>
```



# Do notation

Sequencing IO actions can also be done using the `do` notation.



`greet :: IO ()`  greets the user:

```haskell
greet = do
  putStr "Please enter your name: "
  getLine
  putStrLn "Hello, there!"
```

It asks for your name but shows the same greeting regardless of input:

```haskell
> greet
Please enter your name: Stefan   -- input followed by <enter>
Hello, there!
```



`personalGreet :: IO ()` offers a more personal touch:

```haskell
personalGreet = do
  putStr "Please enter your name: "
  name <- getLine
  putStrLn ("Hello, " ++ name)
```

It doesn't ignore the input given:

```haskell
> personalGreet
Please enter your name: Stefan
Hello, Stefan
```



## Relation to bind `>>=`

* `var <- expr ...` becomes `expr >>= \var -> ...`
* `e ...` becomes `e >> ...`

For example,

```haskell
do {
  var1 <- expr1;
  var2 <- expr2;
  expr3;
  expr4;
}
```

becomes:

```haskell
expr1 >>= \var1 ->
expr2 >>= \var2 ->
expr3 >>
expr4
```

Brackets `{ }` create a separating block. Semicolons `;` separate multiple actions are written on the same row.



`loudEcho` rewritten in do notation:

```haskell
loudEcho' :: IO ()
loudEcho' = do
  line <- getLine
  if line == "." then  -- stop the loop on period
    return ()
  else do
    putStr (map toUpper line)  -- "yell" the input
    putStrLn "!!"
    loudEcho'  -- keep looping
```